## System verbs

With our first object created and server properties prepared, we can focus a little on the various database verbs the server can call.

#### System Object `#0`

The System Object can contain several verbs that can implement custom behavior for specific events in the server:

- `server_started()`
- `checkpoint_started()`
- `checkpoint_finished(success)`
- `handle_task_timeout(resource, traceback, formatted)`
- `handle_uncaught_error(code, msg, value, stack, traceback)`

The following list can also be overridden on other network listener objects:

* `do_login_command(args)`
* `do_command(args)`
* `do_out_of_band_command(args)`
* `user_connected(user)`
* `user_created(user)`
* `user_reconnected(user)`
* `user_disconnected(user)`
* `user_client_disconnected(user)`

#### Generic Objects

Any object can implement the following verbs:

- `initialize()`
- `recycle()`
- `ownership_quota`

#### Containers

These verbs are for events triggered by moving objects from one location to another:

- `accept()`
- `enterfunc()`
- `exitfunc()`

For debugging purposes, it can be very useful if these have some generic implementation that does nothing besides logging the event in the server log. However, some care is in order. We can't simply return ok and expect everything to function afterwards. 

The `do_command` verb is a good example. This command allows for database driven command parsing. The changelog and manual indicate this verb should return false in order for the server to continue parsing. If this method returns true, the server stops. This could have disastrous consequences and leave your MOO unusable.

Let's implement some. Note that there is no hard requirement to do so and these implementations will make the server logs quite chatty. While we're at it, we can peek at LamdaCore's implementations for some of them to integrate the generic guards to prevent them from doing anything useful when called directly:

```
.program #0:do_login_command
if (callers())
  return E_PERM;
endif
server_log("do_login_command");
return #2;
.
```
```
;add_verb(#0, {#2, "rxd", "server_started"}, {"this", "none", "this"})
.program #0:server_started
if (!callers())
  server_log("server_started");
endif
.
```
```
;add_verb(#0, {#2, "rxd", "checkpoint_started"}, {"this", "none", "this"})
.program #0:checkpoint_started
callers() && raise(E_PERM);
server_log("checkpoint_started");
.
```
```
;add_verb(#0, {#2, "rxd", "checkpoint_finished"}, {"this", "none", "this"})
.program #0:checkpoint_finished
callers() && raise(E_PERM);
server_log("checkpoint_finished: " + tostr(args[1]));
.
```
```
;add_verb(#0, {#2, "rxd", "user_created user_connected"}, {"this", "none", "this"})
.program #0:user_created
if (callers())
  return;
endif
server_log("user_created/user_connected: " + tostr(args[1]));
.
```
```
;add_verb(#0, {#2, "rxd", "user_reconnected"}, {"this", "none", "this"})
.program #0:user_reconnected
if (callers())
  return;
endif
server_log("user_reconnected: " + tostr(args[1]));
.
```
```
;add_verb(#0, {#2, "rxd", "user_disconnected user_client_disconnected"}, {"this", "none", "this"})
.program #0:user_disconnected
if (callers())
  return;
endif
server_log("user_disconnected/user_client_disconnected: " + tostr(args[1]));
.
```
```
;add_verb(#0, {#2, "rxd", "do_command"}, {"this", "none", "this"})
.program #0:do_command
if (callers())
  return E_PERM;
endif
server_log("do_command: " + argstr);
.
```
```
;add_verb(#0, {#2, "rxd", "do_out_of_band_command"}, {"this", "none", "this"})
.program #0:do_out_of_band_command
if (caller == #-1 && caller_perms() == #-1 && callers() == {})
  server_log("do_out_of_band_command: " + argstr);
else
  return E_PERM;
endif
.
```
```
;add_verb(#0, {#2, "rxd", "handle_uncaught_error"}, {"this", "none", "this"})
.program #0:handle_uncaught_error
if (!callers())
  {code, msg, value, stack, traceback} = args;
  server_log("handle_uncaught_error: " + msg);
endif
.
```
```
;add_verb(#0, {#2, "rxd", "handle_task_timeout"}, {"this", "none", "this"})
.program #0:handle_task_timeout
if (!callers())
  {resource, traceback, formatted} = args;
  server_log("handle_task_timeout: " + resource);
endif
.
```

With some of these base methods implemented, we gain valuable insight in server driven calls to the System Object's verbs and can continue onwards in our journey of the minimal.
